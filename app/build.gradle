plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

def alias = 'com.documentsui.shortcut'
def password = 'a-password'
def fileName = 'com.documentsui.shortcut.keystore'

tasks.register('deleteKeystore', Delete) {
    delete fileName
}
tasks.register('generateCertificates', Exec) {
    commandLine 'keytool', '-genkey', '-v', '-keystore', fileName, '-storepass', password, '-alias', alias, '-keypass', password, '-keyalg', 'RSA', '-keysize', '2048', '-validity', '10000', '-dname', 'CN=Android Debug,O=Android,C=US'
}

generateCertificates.dependsOn deleteKeystore
build.dependsOn generateCertificates

android {
    namespace 'com.documentsui.shortcut'
    compileSdk 33

    defaultConfig {
        applicationId "com.documentsui.shortcut"
        minSdk 26
        targetSdk 33
        versionCode 2
        versionName "1.1"
    }

    signingConfigs {
        release{
            storeFile file(fileName)
            storePassword password
            keyAlias alias
            keyPassword password
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.10.1'
    implementation 'androidx.appcompat:appcompat:1.6.1'
}
